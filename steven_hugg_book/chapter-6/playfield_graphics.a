
	processor 6502
	include "vcs.h"
	include "macro.h"

	org  $f000

; The VCS offers playfield objects. Playfield objects can be 
; 40 pixels. Of these, only 20 are unique; the right half of 
; the screen is either a duplicate or mirror of the 20 lef-hand 
; side pixels.

; The Counter variable is used to control the playfield color
Counter	equ $81

; Use the CLEAN_START macro to init RAM and registers:
Start	CLEAN_START

NextFrame
; Enable VBLANK (disable output)
	lda #2
    sta VBLANK
; At the beginning of the frame we set the VSYNC bit...
	lda #2
	sta VSYNC
; And hold it on for 3 scanlines...
	sta WSYNC
	sta WSYNC
	sta WSYNC
; Now we turn VSYNC off.
	lda #0
	sta VSYNC

; Now we need 37 lines of VBLANK...
	ldx #37
LVBlank	sta WSYNC	; accessing WSYNC stops the CPU until next scanline
	dex		; decrement X
	bne LVBlank	; loop until X == 0

; Re-enable output (disable VBLANK)
	lda #0
        sta VBLANK
; 192 scanlines are visible
	ldx #192
	lda #0		; the playfield pattern, change every scanline
        ldy Counter	; foreground color, change each frame
ScanLoop
	sta WSYNC
	; use the same pattern for all 3 playfield registers
        sta PF0
        sta PF1
        sta PF2
        sta COLUBK	; set background color
        sty COLUPF	; set foreground color
        clc		; always clear carry before any addition operation
        adc #1		; next playfield pattern
	dex
	bne ScanLoop

; Enable VBLANK again
	lda #2
        sta VBLANK
; 30 lines of overscan to complete the frame
	ldx #30
LVOver	sta WSYNC
	dex
	bne LVOver

; Go back and do another frame
	inc Counter	; effect: change front color
	jmp NextFrame
	
	org $fffc
	.word Start
	.word Start
